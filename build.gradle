/*
 Copyright 2023 Thomas Rosenau

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 */

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '1.15.0'
    id 'org.jetbrains.grammarkit' version '2022.3.2'
}

wrapper {
    gradleVersion = '8.3'
}

Properties releaseProperties = new Properties()
def releasePropertiesFile = file('release.properties')
if (releasePropertiesFile.exists()) {
    releasePropertiesFile.withReader('UTF-8') {
        releaseProperties.load(it)
        project.ext.releaseProps = releaseProperties
    }
} else {
    project.ext.releaseProps = releaseProperties
}

group = 'de.thomasrosenau'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

// version of this plugin
version = '2.1.12'

// IDE version that will be used for compilation, unit tests, and for :runIde task
intellij.version = '233-EAP-SNAPSHOT'

patchPluginXml {
    sinceBuild = '233'
    untilBuild = '233.*'

    // language=HTML prefix=<body> suffix=</body>
    pluginDescription = '''
        <h1>Syntax highlighting for .diff files and .patch files</h1>
        <p>Supports the common formats: normal, contextual, unified, git patch.</p>
        <p>Does not support the formats: side-by-side, diff3, ed, if-else, RCS.</p>
        <br/>
        <hr/>
        <p>Copyright 2023 Thomas Rosenau</p>
        <p>Licensed under the Apache License, Version 2.0 (the "License"); you may not use this software except in
           compliance with the License. You may obtain a copy of the License at</p>
        <br/>
        <p><a href="https://www.apache.org/licenses/LICENSE-2.0">https://www.apache.org/licenses/LICENSE-2.0</a></p>
        <br/>
        <p>Unless required by applicable law or agreed to in writing, software distributed under the License is distributed
           on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for
           the specific language governing permissions and limitations under the License.</p>
        <hr/>
        <br/>
    '''

    // language=HTML prefix=<body> suffix=</body>
    changeNotes = '''
        <h5>2.1.12</h5>
        <ul>
          <li>Update compatibility for 2023.3+ product lines</li>
        </ul>
        <h5>2.1.11</h5>
        <ul>
          <li>Update compatibility for 2023.2+ product lines</li>
        </ul>
        <h5>2.1.10</h5>
        <ul>
          <li>Update compatibility for 2023.1+ product lines</li>
        </ul>
        <h5>2.1.9</h5>
        <ul>
          <li>Update compatibility for 2022.3+ product lines</li>
        </ul>
        <h5>2.1.8</h5>
        <ul>
          <li>Update compatibility for 2022.2+ product lines</li>
        </ul>
        <h5>2.1.7</h5>
        <ul>
          <li>Update compatibility for 2021.1+ product lines</li>
        </ul>
        <h5>2.1.6</h5>
        <ul>
          <li>Switch to Java 11</li>
          <li>Update compatibility for 2020.3 product line</li>
          <li>Replace deprecated icon loader call</li>
        </ul>
        <h5>2.1.5</h5>
        <ul>
          <li>Update compatibility for 2020.2 product line</li>
        </ul>
        <h5>2.1.4</h5>
        <ul>
          <li>Update compatibility for 2020.1 product line</li>
        </ul>
        <h5>2.1.3</h5>
        <ul>
          <li>Update compatibility for 2019.3 product line</li>
        </ul>
        <h5>2.1.2</h5>
        <ul>
          <li>Update compatibility for 2019.2 product line</li>
        </ul>
        <h5>2.1.1</h5>
        <ul>
          <li>Fix Autoscroll from Source in Structure View</li>
        </ul>
        <h5>2.1.0</h5>
        <ul>
          <li>Structure View</li>
          <li>Support for git binary patches</li>
          <li>Fix git patch footer detection</li>
        </ul>
        <h5>2.0.0</h5>
        <ul>
          <li>Folding support</li>
          <li>Stricter syntax</li>
        </ul>
        <h5>1.2.0</h5>
        <ul>
          <li>Support highlighting of git patch headers</li>
        </ul>
        <h5>1.1.0</h5>
        <ul>
          <li>Support highlighting of file names, separators, EOL hints, and modified lines</li>
          <li>Color settings page</li>
          <li>Icon changed</li>
        </ul>
    '''
}

def namespacePath = 'de/thomasrosenau/diffplugin'

def genSrcPath = 'src/main/generated-java'
File genSrc = file genSrcPath
sourceSets.main.java.srcDir genSrc
idea.module.generatedSourceDirs += genSrc

tasks {
    generateLexer {
        sourceFile.set(file("src/main/java/${namespacePath}/lexer/Diff.flex"))
        targetDir.set("${genSrcPath}/${namespacePath}")
        targetClass.set("DiffLexer")
        purgeOldFiles.set(true)
    }
    generateParser {
        sourceFile.set(file("src/main/java/${namespacePath}/parser/Diff.bnf"))
        targetRoot.set(genSrcPath)
        pathToParser.set("DiffParser.java")
        pathToPsiRoot.set("${namespacePath}/psi")
        purgeOldFiles.set(true)
    }
}

compileJava {
    dependsOn generateLexer, generateParser
}

clean {
    delete genSrcPath
}

def intellijPublishToken = project.ext.releaseProps['intellijPublishToken'] ?:
        'see https://plugins.jetbrains.com/docs/intellij/deployment.html'
publishPlugin {
    token = intellijPublishToken
}

test {
    intellij.plugins = ['java']
}

repositories {
    mavenCentral()
}
